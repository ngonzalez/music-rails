
class App.AudioFiles
    constructor: (@data) ->
        @intervals = {}
        @css = <%= FA_CSS.to_json.html_safe %>
        @observeRows()

    observeRows: () ->
        $.each $('.' + @css.play), (i, element) =>
            $(element).click (e) =>
                $('.' + @css.play).off 'click'
                @handleClickEvent $(element),
                    @data.find (item) -> item.id == parseInt($(element).data('id'))

    handleClickEvent: (element, item) ->
        @toggleIcon element, @css.processing
        @intervals[item.id] = setInterval () =>
            $.get item.url, (response) =>
                if response.m3u8_exists
                    @toggleIcon element, @css.processing
                    @observeRows()
                    clearInterval @intervals[item.id]
                    window.location = response.stream_url
        , 3000

    toggleIcon: (element, className) ->
        element.toggleClass @css.hidden
        element.parent().find('.' + className).toggleClass @css.hidden
